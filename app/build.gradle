apply plugin: 'com.android.application'

android {
    signingConfigs {
        debug {
            def props = new Properties()
            def propsFile = rootProject.file("debug.properties");
            if (propsFile.exists()) {
                props.load(new FileInputStream(propsFile))
                android.signingConfigs.debug.storeFile rootProject.file(props.keyStore)
                android.signingConfigs.debug.storePassword props.keyStorePassword
                android.signingConfigs.debug.keyAlias props.keyAlias
                android.signingConfigs.debug.keyPassword props.keyAliasPassword
            }

        }
        release {
            def props = new Properties()
            def propsFile = rootProject.file("release.properties");
            if (propsFile.exists()) {
                props.load(new FileInputStream(propsFile))
                android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
                android.signingConfigs.release.storePassword props.keyStorePassword
                android.signingConfigs.release.keyAlias props.keyAlias
                android.signingConfigs.release.keyPassword props.keyAliasPassword
            }
        }
    }

    compileSdkVersion 27
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "cz.dvratil.fbeventsync"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 17
        versionName "0.15"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    productFlavors {
    }
    lintOptions {
        baseline file("lint-baseline.xml")
    }

    testOptions {
        unitTests {
            all {
                ignoreFailures false
                testLogging {
                    events "passed", "skipped", "failed", "standardOut", "standardError"
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.loopj.android:android-async-http:1.4.9'
    implementation 'com.android.support:design:27.0.2'
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation 'net.sf.biweekly:biweekly:0.6.1'
    implementation 'com.github.kizitonwose.colorpreference:core:1.0.5'
    implementation 'com.larswerkman:lobsterpicker:1.0.1'

    debugImplementation 'com.amitshekhar.android:debug-db:1.0.1'

    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.tngtech.java:junit-dataprovider:1.13.1'
    testImplementation 'org.json:json:20171018'
}
